include "math.umi"
include "SDL2.umi"

alias WINDOW_WIDTH = 1000
alias WINDOW_HEIGHT = 800
alias WINDOW_WIDTH_F = Float(WINDOW_WIDTH)
alias WINDOW_HEIGHT_F = Float(WINDOW_HEIGHT)

class Paddle {
    mut SDL_RectF rect
    Int up_scancode
    Int down_scancode
    mut Int score
    Text score_text

    Paddle(Float x, Int up_scancode, Int down_scancode, Int text_pos, Pointer renderer) {
        rect.x = x
        rect.y = WINDOW_HEIGHT_F / 2.0f - HEIGHT / 2.0f
        rect.w = WIDTH
        rect.h = HEIGHT
        this.up_scancode = up_scancode
        this.down_scancode = down_scancode
        score_text = Text(text_pos - 50, 20, "0", renderer)
    }

    static Bool within(Float point, Float left_bound, Float width) {
        left_bound <= point && point <= left_bound + width
    }

    # TODO: fix
    Bool collidingWithBall(SDL_RectF ball_rect) {
        if Ball.game_over { return false }
        within(ball_rect.x, rect.x, WIDTH) && within(ball_rect.y, rect.y, HEIGHT)
    }

    Void handleKeys(Array[Byte] pressed_keys) {
        mut Float change = 0.0f

        if pressed_keys.get(up_scancode) == 1 { change = change - SPEED }
        if pressed_keys.get(down_scancode) == 1 { change = change + SPEED }

        rect = changeRectF(rect, 0.0f, change, WIDTH, HEIGHT)
    }

    Void increaseScore(Pointer renderer) {
        score = score + 1
        score_text.updateText(score.toString(), renderer)
    }

    Void render(Pointer renderer) {
        score_text.render(renderer)
        if !(Ball.game_over) { handleError(SDL.renderFillRectF(renderer, ref rect)) }
    }

    alias WIDTH = 20.0f
    alias HEIGHT = 130.0f
    alias PADDING = 30.0f
    alias SPEED = 12.0f
}

class Ball {
    mut SDL_RectF rect
    mut Float velocity_x
    mut Float velocity_y

    Ball() { resetPositionAndVelocity() }

    Void resetPositionAndVelocity() {
        rect = CENTRE
        mut Double angle = random.double() * 2. * PI / 3. - PI / 3.
        if random.int(2) == 1 { angle = angle + PI }
        velocity_x = SPEED * Float(cos(angle))
        velocity_y = SPEED * Float(sin(angle))
    }

    Void move() {
        rect = changeRectF(rect, velocity_x, velocity_y, SIZE, SIZE)
    }

    Void verticalWallCollision(Paddle paddle, Pointer renderer) {
        if game_over {
            velocity_x = -velocity_x
            return
        }
        resetPositionAndVelocity()
        paddle.increaseScore(renderer)
        score_sound.play()
        if paddle.score == 11 { game_over = true }
    }

    Void handleCollision(Paddle left_paddle, Paddle right_paddle, Pointer renderer) {
        if rect.y == 0.0f || rect.y == WINDOW_HEIGHT_F - SIZE {
            velocity_y = -velocity_y
        }

        if rect.x == 0.0f {
            verticalWallCollision(right_paddle, renderer)
        } else if rect.x == WINDOW_WIDTH_F - SIZE {
            verticalWallCollision(left_paddle, renderer)
        } 

        if left_paddle.collidingWithBall(rect) || right_paddle.collidingWithBall(rect) {
            velocity_x = -velocity_x
            hit_sound.play()
        }
    }

    Void render(Pointer renderer) {
        handleError(SDL.renderFillRectF(renderer, ref rect))
    }

    alias SIZE = 20.0f
    alias SPEED = 10.0f
    
    static Random random
    static SDL_RectF CENTRE
    static Sound hit_sound
    static Sound score_sound
    static mut Bool game_over

    static Ball() {
        random = Random()
        CENTRE.x = WINDOW_WIDTH_F / 2.0f - SIZE / 2.0f
        CENTRE.y = CENTRE.x
        CENTRE.w = SIZE
        CENTRE.h = SIZE
        hit_sound = Sound("hit.wav")
        score_sound = Sound("score.wav")
    }
}

class Text {
    mut Pointer surface
    mut Pointer texture
    mut SDL_Rect rect

    Text(Int x, Int y, String inital_text, Pointer renderer) {
        rect.x = x
        rect.y = y
        rect.w = 100
        rect.h = 100
        updateText(inital_text, renderer)
    }

    Void updateText(String new_text, Pointer renderer) {
        SDL.freeSurface(surface)
        SDL.destroyTexture(texture)
        surface = TTF.renderTextSolid(FONT, new_text, WHITE)
        texture = SDL.createTextureFromSurface(renderer, surface)
    }

    Void render(Pointer renderer) {
        handleError(SDL.renderCopy(renderer, texture, Pointer.null(), ref rect))
    }

    static Pointer FONT
    static SDL_Color WHITE

    static Text() {
        TTF.init()
        FONT = TTF.openFont("FFF_Forward.ttf", 24)
        WHITE.r = Byte(255)
        WHITE.g = Byte(255)
        WHITE.b = Byte(255)
    }
}

class Sound {
    mut Pointer wav_buffer
    mut UInt wav_length
    mut UInt device_id

    Sound(String file_path) {
        mut SDL_AudioSpec wav_spec = wav_spec
        SDL.loadWAV(file_path, ref wav_spec, ref wav_buffer, ref wav_length)

        mut SDL_AudioSpec _ = _ # want to pass null but can't
        device_id = SDL.openAudioDevice(Pointer.null(), 0, ref wav_spec, ref _, 0)
        SDL.pauseAudioDevice(device_id, 0) # actually unpause it
    }

    Void play() {
        handleError(SDL.queueAudio(device_id, wav_buffer, wav_length))
    }

    Void destroy() {
        SDL.closeAudioDevice(device_id)
        SDL.freeWAV(wav_buffer)
    }
}

Void handleError(Int status) {
    if status < 0 {
        print(SDL.getError())
        exit(status)
    }
}

SDL_RectF changeRectF(SDL_RectF rect, Float change_x, Float change_y, Float obj_width, Float obj_height) {
    rect.x = clamp(rect.x + change_x, 0.0f, WINDOW_WIDTH_F - obj_width)
    rect.y = clamp(rect.y + change_y, 0.0f, WINDOW_HEIGHT_F - obj_height)
    rect
}

Array[Byte] getPressedKeys() {
    mut Int array_size = 0
    Pointer keys_ptr = SDL.getKeyboardState(ref array_size)
    Array[Byte] pressed_keys = Array[Byte](array_size)
    marshalCopy(keys_ptr, pressed_keys, 0, array_size)
    pressed_keys
}

Void drawMiddleLine(Pointer renderer) {
    mut SDL_Rect line = line
    line.x = WINDOW_WIDTH / 2 - 1
    line.y = 3
    line.w = 2
    line.h = 15
    while line.y < WINDOW_HEIGHT {
        Int err = SDL.renderFillRect(renderer, ref line)
        line.y = line.y + 30
    }
}

Void main() {
    if SDL.init(SDL.INIT_VIDEO | SDL.INIT_AUDIO) < 0 {
        print("Video initialisation failed")
        exit(1)
    }

    Pointer window = SDL.createWindow("Pong", SDL.WINDOWPOS_CENTERED, SDL.WINDOWPOS_CENTERED, WINDOW_WIDTH, WINDOW_HEIGHT, SDL_WindowFlags.SHOWN)

    Pointer renderer = SDL.createRenderer(window, -1, SDL_RendererFlags.ACCELERATED)

    Paddle left_paddle = Paddle(Paddle.PADDING, SDL.SCANCODE_W, SDL.SCANCODE_S, WINDOW_WIDTH / 4, renderer)
    Float right_paddle_x = WINDOW_WIDTH_F - Paddle.PADDING - Paddle.WIDTH
    Paddle right_paddle = Paddle(right_paddle_x, SDL.SCANCODE_UP, SDL.SCANCODE_DOWN, 3 * WINDOW_WIDTH / 4, renderer)
    Ball ball = Ball()

    mut Bool quit = false
    while !quit {
        mut SDL_Event e = e
        while SDL.pollEvent(ref e) > 0 {
            if e.type == SDL_EventType.QUIT { quit = true }
        }

        Array[Byte] pressed_keys = getPressedKeys()
        left_paddle.handleKeys(pressed_keys)
        right_paddle.handleKeys(pressed_keys)
        ball.move()
        ball.handleCollision(left_paddle, right_paddle, renderer)

        SDL.setRenderDrawColor(renderer, Byte(0), Byte(0), Byte(0), Byte(255))
        SDL.renderClear(renderer)

        # everything except background is white
        SDL.setRenderDrawColor(renderer, Byte(255), Byte(255), Byte(255), Byte(255))
        drawMiddleLine(renderer)
        left_paddle.render(renderer)
        right_paddle.render(renderer)
        ball.render(renderer)

        SDL.renderPresent(renderer)

        SDL.delay(UInt(1000 / 60))
    }

    SDL.destroyRenderer(renderer)
    SDL.destroyWindow(window)
    TTF.quit()
    SDL.quit()
}
