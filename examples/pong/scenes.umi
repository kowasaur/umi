include "text.umi"
include "sound.umi"
include "paddle.umi"
include "ball.umi"

# This is an abstract class
class Scene {
    # Update state and return either the same or a new Scene
    Scene nextScene(Array[Byte] pressed_keys, Pointer renderer) { this }
    # Only rendering should be done here
    Void render(Pointer renderer) {}
}

class MenuOption {
    Text text
    mut SDL_Rect border_rect
    Scene scene

    MenuOption(Int y, Int w, String text_str, Pointer renderer, Scene scene) {
        this.scene = scene
        alias HEIGHT = 90
        alias BORDER_W = 300
        alias BORDER_X = WINDOW_WIDTH / 2 - BORDER_W / 2
        border_rect = newRect(BORDER_X, y, BORDER_W, HEIGHT)

        alias text_x = WINDOW_WIDTH / 2 - w / 2
        alias PAD = 15
        text = Text(newRect(text_x, y + PAD, w, HEIGHT - PAD * 2), text_str, renderer)
    }

    Void changeTextColor(SDL_Color color, Pointer renderer) {
        text.updateText(text.text_str, color, renderer)
    }

    Void render(Pointer renderer, Bool selected) {
        if selected { SDL.renderFillRect(renderer, ref border_rect) } 
        else { SDL.renderDrawRect(renderer, ref border_rect) }
        text.render(renderer)
    }
}

class Menu : Scene {
    Text title
    Array[MenuOption] options
    mut Int selected # index in options
    mut Int ticks_since_press # so it moves slow enough

    Menu(Pointer renderer) {
        alias TITLE_W = 450
        alias TITLE_X = WINDOW_WIDTH / 2 - TITLE_W / 2
        title = Text(newRect(TITLE_X, 150, TITLE_W, 180), "Pong", renderer)

        options = Array[MenuOption](3)
        options.set(0, MenuOption(440, 230, "1 Player", renderer, Court1Player(renderer)))
        options.set(1, MenuOption(550, 250, "2 Players", renderer, Court2Players(renderer)))
        options.set(2, MenuOption(660, 120, "Quit", renderer, this))

        changeSelected(0, renderer)
    }

    Void changeSelected(Int change, Pointer renderer) {
        options.get(selected).changeTextColor(Text.WHITE, renderer)
        selected = clamp(selected + change, 0, options.size() - 1)
        options.get(selected).changeTextColor(Text.BLACK, renderer)
        ticks_since_press = 0
    }

    Scene nextScene(Array[Byte] pressed_keys, Pointer renderer) {
        if pressed_keys.get(SDL.SCANCODE_RETURN) == 1 {
            if selected == options.size() - 1 { quit = true }
            return options.get(selected).scene
        }

        if ticks_since_press > 10 {
            if pressed_keys.get(SDL.SCANCODE_UP) == 1 { changeSelected(-1, renderer) }
            if pressed_keys.get(SDL.SCANCODE_DOWN) == 1 { changeSelected(1, renderer) }
        } else {
            ticks_since_press = ticks_since_press + 1
        }
        this
    }

    Void render(Pointer renderer) {
        title.render(renderer)
        mut Int i = 0
        while i < options.size() {
            options.get(i).render(renderer, i == selected)
            i = i + 1
        }
    }

    static mut Bool quit
}

class Court2Players : Scene {
    Paddle left_paddle
    Paddle right_paddle
    Ball ball

    Court2Players(Pointer renderer) {
        left_paddle = Paddle(Paddle.PADDING, SDL.SCANCODE_W, SDL.SCANCODE_S, WINDOW_WIDTH / 4, renderer)
        alias right_paddle_x = WINDOW_WIDTH_F - Paddle.PADDING - Paddle.WIDTH
        right_paddle = Paddle(right_paddle_x, SDL.SCANCODE_UP, SDL.SCANCODE_DOWN, 3 * WINDOW_WIDTH / 4, renderer)
        ball = Ball()
    }

    # Returns true if a player scored 11
    Bool verticalWallCollision(Paddle paddle, Pointer renderer) {
        ball.resetPositionAndVelocity()
        paddle.increaseScore(renderer)
        score_sound.play()
        paddle.score == 11
    }

    # Returns whether to end the game
    Bool handleCollision(Pointer renderer) {
        ball.handleHorizontalWallCollision()

        if ball.collidingWithLeftWall() {
            if verticalWallCollision(right_paddle, renderer) { return true }
        } else if ball.collidingWithRightWall() {
            if verticalWallCollision(left_paddle, renderer) { return true }
        }

        if left_paddle.collidingWithBall(ball.rect) || right_paddle.collidingWithBall(ball.rect) {
            ball.velocity_x = -(ball.velocity_x)
            hit_sound.play()
        }

        false
    }

    Scene nextScene(Array[Byte] pressed_keys, Pointer renderer) {
        if pressed_keys.get(SDL.SCANCODE_ESCAPE) == 1 { return Menu(renderer) }
        left_paddle.handleKeys(pressed_keys)
        right_paddle.handleKeys(pressed_keys)
        ball.move()
        if handleCollision(renderer) { return GameOver(this) }
        this
    }

    Void render(Pointer renderer) {
        drawMiddleLine(renderer)
        left_paddle.render(renderer)
        right_paddle.render(renderer)
        ball.render(renderer)
    }

    static Sound hit_sound
    static Sound score_sound

    static Court2Players() {
        hit_sound = Sound("assets/hit.wav")
        score_sound = Sound("assets/score.wav")
    }
}

class Court1Player : Court2Players {
    mut Byte w_pressed
    mut Byte s_pressed
    mut Int future_w_pressed
    mut Int future_s_pressed
    mut Int ticks

    Court1Player(Pointer renderer) { 
        base(renderer)
        ticks = 1
    }

    Scene nextScene(Array[Byte] pressed_keys, Pointer renderer) {
        if ticks == 0 && ball.velocity_x < 0.0f {
            w_pressed = Byte(future_w_pressed)
            s_pressed = Byte(future_s_pressed)
            future_w_pressed = 0
            future_s_pressed = 0

            # have to do because Umi currently can't do ball.rect.x since ball.rect is not just an identifier
            SDL_RectF ball_rect = ball.rect
            SDL_RectF paddle_rect = left_paddle.rect

            alias x_diff = ball_rect.x - paddle_rect.x
            # this does not take into account bouncing off the walls
            Float predicted_y = ball_rect.y - x_diff * ball.velocity_y / ball.velocity_x
            
            if predicted_y < paddle_rect.y { future_w_pressed = 1 }
            else if predicted_y > paddle_rect.y + Paddle.HEIGHT { future_s_pressed = 1 }
        }

        pressed_keys.set(SDL.SCANCODE_W, w_pressed)
        pressed_keys.set(SDL.SCANCODE_S, s_pressed)
        ticks = (ticks + 1) % DELAY
        base.nextScene(pressed_keys, renderer)
    }

    alias DELAY = 7
}

class GameOver : Scene {
    Ball ball
    Text left_paddle_text
    Text right_paddle_text

    GameOver(Court2Players court) { 
        ball = court.ball 
        left_paddle_text = court.left_paddle.score_text
        right_paddle_text = court.right_paddle.score_text
    }

    Scene nextScene(Array[Byte] pressed_keys, Pointer renderer) {
        if pressed_keys.get(SDL.SCANCODE_ESCAPE) == 1 { return Menu(renderer) }
        ball.move()  
        ball.handleHorizontalWallCollision()
        if ball.collidingWithLeftWall() || ball.collidingWithRightWall() {
            ball.velocity_x = -(ball.velocity_x)
        }
        this
    }

    Void render(Pointer renderer) {
        drawMiddleLine(renderer)
        left_paddle_text.render(renderer)
        right_paddle_text.render(renderer)
        ball.render(renderer)
    }
}
